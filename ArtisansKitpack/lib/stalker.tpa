COPY ~%MOD_FOLDER%/Ranger/Stalker/2DA~ ~OVERRIDE~
COPY ~%MOD_FOLDER%/Ranger/Stalker/spells~ ~OVERRIDE~

ACTION_IF MOD_IS_INSTALLED ~Setup-ArtisansKitpack.tp2~ ~2000~ THEN BEGIN

LAF ~GET_KIT_STRREF~ STR_VAR kit_name = ~STALKER~ RET kit_strref END
STRING_SET_EVALUATE kit_strref ~STALKER: Stalkers serve as covert intelligence-gatherers, comfortable in both wilderness and urban settings. They are spies, informants, and interrogators. Their versatile set of skills allow them to take on any mission, whether it involves persuasion, assassination, or open combat.

Advantages:
– +10% bonus to Set Traps.
– Gains Camouflage bonuses to Hide in Shadows and Move Silently (+25%) while indoors.
– May use Tracking while indoors.
– Fleet of Foot: The stalker moves at 30% increased movement rate while stealthed or invisible.
– May use Backstab ability, although for a lower damage multiplier than Thieves:
  Level 1-8: x2
  Level 9-16: x3
  Level 17+: x4
– May use the Analyze ability at will.

ANALYZE: With this ability, the user carefully observes the target and identifies their strengths and weaknesses, revealing their Hit Die, THAC0, Saving Throws, Magic Resistance, Damage Resistance and vulnerability to Poison and Backstabs. This ability may be used an unlimited number of times.

– 8th level: May memorize one Mage spell per spell level: Friends (1st level), Detect Invisibility (2nd level), Haste (3rd level)
– 15th level: Cloak of Shadows: The stalker's mastery of stealth renders <PRO_HIMHER> completely undetectable and untargetable by all means, whether magical or mundane, while hiding or Invisible. Even creatures that normally see through invisibility cannot perceive the stalker.

Disadvantages:
– Hit Die: d8
– May not wear armor heavier than studded leather.~

// STALKER - HIT DIE
COPY_EXISTING ~hpclass.2da~ OVERRIDE
REPLACE_TEXTUALLY CASE_INSENSITIVE ~STALKER              HPWAR~ ~STALKER              HPARCH~

// STALKER - HLA
COPY_EXISTING ~luabbr.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~STALKER~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 1 cols ~RA2~
	  END
	END
	
COPY_EXISTING ~LURA2.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_SPCL907     *          *         15        40         20~ ~GA_SPCL916     *          *         15        40         1~
	
APPEND ~splprot.2da~ ~C0_OUTDOORS_CHECK%TAB%0x106%TAB%1%TAB%8~ UNLESS ~C0_OUTDOORS_CHECK~
COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ // read column value
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~C0_OUTDOORS_CHECK~ BEGIN
	    SET C0_OUTDOORS_CHECK = %row%
	  END
	END

COPY_EXISTING ~c0stk00.eff~ ~override~
	WRITE_SHORT 0x20 C0_OUTDOORS_CHECK

COPY_EXISTING ~C0STK01.SPL~ OVERRIDE
SAY NAME1 ~Tracking~
SAY UNIDENTIFIED_DESC ~Tracking
With an intimate knowledge of <PRO_HISHER> surroundings and the creatures that live within them, a ranger can use the Tracking ability to give <PRO_HIMHER>self a general idea of what creatures are in an area and which direction they are. All enemies within a 100-ft. radius of the ranger will be revealed from within the explored fog of war.

This ability only works while outdoors.~
  
DEFINE_ACTION_FUNCTION cd_new_portrait_icon
  INT_VAR string = 0
  STR_VAR bam_file = "****"
  RET     icon
  BEGIN

  COPY_EXISTING ~statdesc.2da~ ~override~
    COUNT_2DA_ROWS 3 count
    READ_2DA_ENTRY (count - 1) 0 3 icon
    SET icon += 1

  APPEND ~statdesc.2da~ ~%icon% %string% %bam_file%~ UNLESS ~%bam_file%~

END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Tracking~) STR_VAR bam_file = SPCL922I RET SPCL922I = icon END

COPY_EXISTING ~C0STK01A.SPL~ OVERRIDE
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = SPCL922I END

COPY_EXISTING ~C0STK01C.SPL~ OVERRIDE
	LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (~Tracking Deactivated~) END

COPY_EXISTING ~thiefscl.2da~ ~override~
  REPLACE_TEXTUALLY ~^\([ %TAB%]*MAGE\)~ ~CD_DELETE_ME \1~ // adding column to header column
  COUNT_2DA_COLS col_count
  FOR (col = 1 ; col < col_count ; ++col) BEGIN
    READ_2DA_ENTRY 0 col col_count kit
    PATCH_IF ("%kit%" STRING_COMPARE_CASE "STALKER" = 0) BEGIN
      READ_2DA_ENTRY 7 col col_count val
      SET_2DA_ENTRY  7 col col_count 100
      SET col = col_count // kill loop
    END
  END  
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~ // removing it
  PRETTY_PRINT_2DA
  BUT_ONLY
  
COPY_EXISTING ~clasiskl.2da~ ~override~
  REPLACE_TEXTUALLY ~^\([ %TAB%]*MAGE\)~ ~CD_DELETE_ME \1~ // adding column to header column
  COUNT_2DA_COLS col_count
  FOR (col = 1 ; col < col_count ; ++col) BEGIN
    READ_2DA_ENTRY 0 col col_count kit
    PATCH_IF ("%kit%" STRING_COMPARE_CASE "STALKER" = 0) BEGIN
      READ_2DA_ENTRY 7 col col_count val
      SET_2DA_ENTRY  7 col col_count (val+35)
      SET col = col_count // kill loop
    END
  END  
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~ // removing it
  PRETTY_PRINT_2DA
  BUT_ONLY

END
ACTION_IF NOT MOD_IS_INSTALLED ~Setup-ArtisansKitpack.tp2~ ~2000~ THEN BEGIN

COPY ~%MOD_FOLDER%/Ranger/Stalker/vanilla~ ~OVERRIDE~

LAF ~GET_KIT_STRREF~ STR_VAR kit_name = ~STALKER~ RET kit_strref END
STRING_SET_EVALUATE kit_strref ~STALKER: Stalkers serve as covert intelligence-gatherers, comfortable in both wilderness and urban settings. They are spies, informants, and interrogators. Their versatile set of skills allow them to take on any mission, whether it involves persuasion, assassination, or open combat.

Advantages:
– +25% bonus to Hide in Shadows and Move Silently.
– Stealth Caster: The stalker's spells cast while hiding or invisible have their casting time reduced to 1, if higher. 
– May use Backstab ability, although for a lower damage multiplier than Thieves:
  Level 1-8: x2
  Level 9-16: x3
  Level 17+: x4
– May use the Analyze ability at will.

ANALYZE: With this ability, the user carefully observes the target and identifies their strengths and weaknesses, revealing their Hit Die, THAC0, Saving Throws, Magic Resistance, Damage Resistance and vulnerability to Poison and Backstabs. This ability may be used an unlimited number of times.

– 8th level: May memorize one Mage spell per spell level: Friends (1st level), Detect Invisibility (2nd level), Haste (3rd level)
– 15th level: Cloak of Shadows: The stalker's mastery of stealth renders <PRO_HIMHER> completely undetectable and untargetable by all means, whether magical or mundane, while hiding or Invisible. Even creatures that normally see through invisibility cannot perceive the stalker.

Disadvantages:
– May not wear armor heavier than studded leather.~

END

// Friends
COPY_EXISTING ~SPWI107.SPL~ ~OVERRIDE/C0STK02.SPL~
WRITE_SHORT 0X34 1
WRITE_SHORT 0X1C 2
// Detect Invisibility
COPY_EXISTING ~SPWI203.SPL~ ~OVERRIDE/C0STK03.SPL~
WRITE_SHORT 0X34 2
WRITE_SHORT 0X1C 2
// Haste
COPY_EXISTING ~SPWI305.SPL~ ~OVERRIDE/C0STK04.SPL~
WRITE_SHORT 0X34 3
WRITE_SHORT 0X1C 2

// VALYGAR

ACTION_IF FILE_EXISTS_IN_GAME ~valyg8.cre~ THEN BEGIN
    COPY_EXISTING ~valyg8.cre~ ~override~
	WRITE_SHORT 0x24 64
	WRITE_SHORT 0x26 64
END

ACTION_IF FILE_EXISTS_IN_GAME ~valyg9.cre~ THEN BEGIN
    COPY_EXISTING ~valyg8.cre~ ~override~
	WRITE_SHORT 0x24 72
	WRITE_SHORT 0x26 72
END

ACTION_IF FILE_EXISTS_IN_GAME ~valyg11.cre~ THEN BEGIN
    COPY_EXISTING ~valyg8.cre~ ~override~
	WRITE_SHORT 0x24 80
	WRITE_SHORT 0x26 80
END

ACTION_IF FILE_EXISTS_IN_GAME ~valyg12.cre~ THEN BEGIN
    COPY_EXISTING ~valyg8.cre~ ~override~
	WRITE_SHORT 0x24 92
	WRITE_SHORT 0x26 92
END

ACTION_IF FILE_EXISTS_IN_GAME ~valyg14.cre~ THEN BEGIN
    COPY_EXISTING ~valyg8.cre~ ~override~
	WRITE_SHORT 0x24 92
	WRITE_SHORT 0x26 92
END

