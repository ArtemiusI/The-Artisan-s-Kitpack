COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ //copies all item files
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
    READ_BYTE    0x1F bard //reads the byte containing the fighter usability flag
    PATCH_IF ((bard BAND 0b01000000) = 0b00000000) BEGIN // if it is usable by bards
      READ_SHORT    0x1C type //reads the byte containing item type
	  READ_SHORT    0x22 appearance //reads the byte containing item appearance
      PATCH_IF (type = 2) AND (appearance = 16690) BEGIN // if it is leather armor
		LPF ALTER_EFFECT
			INT_VAR
				silent = 1
				match_opcode = 145
				opcode = 177
				target = 1
				parameter1 = 202
				parameter2 = 5
				timing = 2
			STR_VAR
				resource = C0ARCAS1
		END
      END
      PATCH_IF (type = 2) AND (appearance = 16691) OR (appearance = 16692) BEGIN // if it is leather armor
		LPF ALTER_EFFECT
			INT_VAR
				silent = 1
				match_opcode = 145
				opcode = 177
				target = 1
				parameter1 = 0
				parameter2 = 2
				timing = 2
			STR_VAR
				resource = C0ARCAS2
		END
      END
    END
  END
BUT_ONLY_IF_IT_CHANGES

COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/2das~ ~override~
COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/spells~ ~override~

ADD_KIT ~C0EK~
  ~C0EK                1           1           1           1           1           1           1           1~
 // appended column-wise to WEAPPROF.2da
//11                                T   S           F   Q             S S
//10                                W   C           L   U             W I
//9                         B L S   O   I   W       A   A             O N
//8                         A O H   H   M   A       I   R C   S       R G
//7         L S           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6         _ _       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5         S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4         W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3         O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2         R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1         D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
 ~C0EK  	2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
 
 // appended to ABCLASRQ.2da
  ~C0EK                12      9       12      15      0       0~
 // appended to ABCLSMOD.2da
  ~C0EK                0       0       0       0       0       0~
 // appended to ABDCDSRQ.2da
  ~C0EK                0       0       0       0       0       0~
 // appended to ABDCSCRQ.2da
  ~C0EK                0       0       0       0       0       0~
 // appended to ALIGNMNT.2da
  ~C0EK                1       1       1       1       1       1       1       1       1~
 // appended to DUALCLAS.2da
  ~C0EK                0       0       0       0       0       0~
 // path to your CLAB-style 2da file 
  ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/2das/c0ek.2da~
  ~K_FM_H K_FM_E K_FM_HE K_FM_HL K_FM_G K_FM_D K_FM_HO~
  ~0x00004000     7~
  ~fm0~
  ~LEAT14 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN4,2 POTN14,5 HAM07 SW1H27 STAF08~
  SAY ~eldritch knight~
  SAY ~Eldritch Knight~
  SAY ~ELDRITCH KNIGHT: Studying the martial and arcane arts to equal degree, the eldritch knight is a versatile combatant who can cast fireball on their foes or charge them with sword drawn. The eldritch knight takes pride in their ability to use the right technique for the job: spells against physically tough foes and force of arms against spellcasting enemies. Eldritch knights split their time between physical training to become better soldiers and arcane study to learn more powerful spells. They tend to be driven individuals, because simultaneously perfecting their spellcasting and combat prowess requires immense time and effort.

Advantages:
– Armored Caster: The Eldritch Knight may cast spells while wearing up to studded leather.
– Combat Casting: The Eldritch Knight is skilled at casting spells in the midst of combat. After making a successful melee attack, the Eldritch Knight's casting speed is increased by 4 for one round. This may only occur once per five rounds.
– 5th level: May use the Eldritch Strike ability once per day. Gains extra uses at levels 8, 11 and 14.

ELDRITCH STRIKE: The Eldritch Knight empowers <PRO_HISHER> next successful attack to weaken their opponent's resistance to their spells, reducing their Saving Throws by 2 and Magic Resistance by 25% for three rounds. This effect is non-cumulative on the same target.

– 8th level: Eldritch Strike reduces Saving Throws by 3 and Magic Resistance by 50%.
– 9th level: May cast Dimension Door twice per day.

DIMENSION DOOR: This spell transports the caster to any place within the visual range of the caster. When the spell is cast, a dimensional portal opens up in front of the caster, who immediately steps through it. Upon passing through the portal, the caster finds <PRO_HIMHER>self at <PRO_HISHER> chosen destination.

– 12th level: Battle Caster: The Eldritch Knight may cast spells while wearing any armor.
– 14th level: Eldritch Strike reduces Saving Throws by 4 and Magic Resistance by 100%.
– 16th level: Improved Combat Casting: Now may occur once per two rounds.

Disadvantages:
– The Eldritch Knight's spells are cast as though a level lower than a normal Mage of the same level.~

LAF fl#add_kit_ee
INT_VAR
		briefdesc = RESOLVE_STR_REF (~ELDRITCH KNIGHT: Studying the martial and arcane arts to equal degree, the eldritch knight is a versatile combatant who can cast fireball on their foes or charge them with sword drawn. The eldritch knight takes pride in their ability to use the right technique for the job: spells against physically tough foes and force of arms against spellcasting enemies.~)
STR_VAR
	kit_name = C0EK
    clswpbon = ~1 0 2~
    clasiskl = ~0 0 0 0 0 0 0~
	clascolr = ~96 50 47 1 3~
    clasthac = ~0~
    clsrcreq = ~1 1 1 1 1 1 1~
END

LAF qd_multiclass
	STR_VAR
		kit_name = C0EK
		kit_clab = C0EK
		base_class = ~F~
		mc_dir = ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/2das~
END

DEFINE_ACTION_FUNCTION cd_new_portrait_icon
  INT_VAR string = 0
  STR_VAR bam_file = "****"
  RET     icon
  BEGIN

  COPY_EXISTING ~statdesc.2da~ ~override~
    COUNT_2DA_ROWS 3 count
    READ_2DA_ENTRY (count - 1) 0 3 icon
    SET icon += 1

  APPEND ~statdesc.2da~ ~%icon% %string% %bam_file%~ UNLESS ~%bam_file%~

END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Combat Casting~) STR_VAR bam_file = c0ek1ic RET c0ek1ic = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Eldritch Strike~) STR_VAR bam_file = c0ek2ic RET c0ek2ic = icon END

COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/spells/c0eknam.spl~ ~override~
LPF ALTER_EFFECT INT_VAR match_opcode = 290 parameter1 = RESOLVE_STR_REF (~Eldritch Knight~) END
COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/spells/c0ek1.spl~ ~override~
LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = c0ek1ic END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_special = 1 parameter1 = RESOLVE_STR_REF (~Combat Casting Active~) END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_special = 2 parameter1 = RESOLVE_STR_REF (~Combat Casting Ready~) END

COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/spells/c0ek2.spl~ ~override~
LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = c0ek2ic END
SAY NAME1 ~Eldritch Strike~
SAY UNIDENTIFIED_DESC ~Eldritch Strike
The Eldritch Knight empowers <PRO_HISHER> next successful attack to weaken their opponent's resistance to their spells, reducing their Saving Throws by 2 and Magic Resistance by 25% for three turns.~

COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/spells/c0ek2a.spl~ ~override~
LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (~Saving Throws and Magic Resistance Lowered~) END

COPY ~%MOD_FOLDER%/MultiKit/Eldritch_Knight/spells/c0ek3.spl~ ~override~
SAY NAME1 ~Dimension Door~
SAY UNIDENTIFIED_DESC ~Dimension Door
(Alteration)

Level: 4
Range: 0
Duration: Instant
Casting Time: 1
Area of Effect: The caster
Saving Throw: None

This spell transports the caster to any place within the visual range of the caster. When the spell is cast, a dimensional portal opens up in front of the caster, who immediately steps through it. Upon passing through the portal, the caster finds <PRO_HIMHER>self at <PRO_HISHER> chosen destination.~