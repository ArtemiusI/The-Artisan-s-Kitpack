COPY ~%MOD_FOLDER%/Paladin/Cavalier/2da~ ~override~
COPY ~%MOD_FOLDER%/Paladin/Cavalier/bams~ ~override~
COPY ~%MOD_FOLDER%/Paladin/Cavalier/spells~ ~override~

// ICONS

DEFINE_ACTION_FUNCTION cd_new_portrait_icon
  INT_VAR string = 0
  STR_VAR bam_file = "****"
  RET     icon
  BEGIN

  COPY_EXISTING ~statdesc.2da~ ~override~
    COUNT_2DA_ROWS 3 count
    READ_2DA_ENTRY (count - 1) 0 3 icon
    SET icon += 1

  APPEND ~statdesc.2da~ ~%icon% %string% %bam_file%~ UNLESS ~%bam_file%~

END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Aura of Chivalry~) STR_VAR bam_file = c0cav01i RET c0cav01i = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Heroic Inspiration~) STR_VAR bam_file = c0cav02i RET c0cav02i = icon END

LAF ~GET_KIT_STRREF~ STR_VAR kit_name = ~CAVALIER~ RET kit_strref END
STRING_SET_EVALUATE kit_strref ~CAVALIER: This class represents the most common picture of the knight: the chivalrous warrior who epitomizes honor, courage, and loyalty. The Cavalier is specialized in battling "classical" evil monsters such as demons and dragons.

Advantages:
– Immunity to Charm, Poison, Fear and Morale Failure.
– +3 bonus to hit and damage rolls against all fiendish and draconic creatures.
– May use the Challenge ability once per day. Gains an additional use every 8th level.

CHALLENGE: The cavalier marks a targeted creature with a Challenge, reducing both the target's and the cavalier's own Armor Class by 10 for the next three rounds. The cavalier deals additional unmitigated magic damage to the Challenged target. Additionally, the Challenged target can be seen within the fog of war and cannot turn invisible for the duration.
  1st-11th level: 1d6 damage
  12th-19th level: 2d6 damage
  20th+ level: 4d6 damage

– 3rd level: Gains Aura of Chivalry.

AURA OF CHIVALRY: The cavalier exudes an aura which inspires their allies to fight at the best of their ability. All allies within visual range of the cavalier become immune to fear and morale failure and gain bonuses in combat.
  3rd - +1 to THAC0.
  11th - +1 to THAC0 and Damage.
  19th - +2 to THAC0 and Damage, +1 to Luck

– 9th level: Gains Heroic Inspiration.

HEROIC INSPIRATION: When the cavalier's maximum hit points falls below 50%, all allies (except the cavalier) within visual range gain an additional +1 bonus to hit, damage and saving throws.

Disadvantages:
– Does not gain Charisma-based Divine Grace bonuses.
– Does not gain Aura of Courage.
– May not Turn Undead.
– May not use ranged weapons and incurs a -4 penalty to hit if wielding a melee/ranged weapon in ranged mode.~

COPY_EXISTING ~C0CAV01.spl~ ~override~
      LPF ADD_SPELL_EFFECT
      INT_VAR
	    opcode = 142
	    target = 2
		timing = 9
        parameter2 = c0cav01i
      END

ADD_PROJECTILE ~%MOD_FOLDER%/spells/C0INAREP.PRO~ ~C0 Instant Area Allies~

COPY_EXISTING ~C0CAV01A.SPL~ OVERRIDE
  LPF ALTER_SPELL_HEADER
	INT_VAR
		projectile = C0INAREP
END

COPY_EXISTING ~C0CAV02.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Gained Passive Ability: Heroic Inspiration~)
END

COPY_EXISTING ~C0CAV02A.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Heroic Inspiration: +1 to THAC0, Damage and Saving Throws to all allies~)
END
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 267
	  parameter1 = RESOLVE_STR_REF (~Heroic Inspiration: +1 to THAC0, Damage and Saving Throws to all allies~)
END
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 142
	  parameter2 = c0cav02i
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~C0CAV03.SPL~ BEGIN

COPY ~%MOD_FOLDER%/spells/challenge~ ~override~

COPY_EXISTING ~C0CAV03.spl~ ~override~
SAY NAME1 ~Challenge!~
SAY UNIDENTIFIED_DESC ~Challenge
The user marks a targeted creature with a Challenge, reducing both the target's and the user's own Armor Class by 10 for the next three rounds. The user deals additional unmitigated magic damage to the Challenged target. Additionally, the Challenged target can be seen within the fog of war and cannot turn invisible for the duration.
  1st-11th level: 2d10 damage
  12th-19th level: 4d10 damage
  20th+ level: 6d10 damage~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Challenged~)
END

END