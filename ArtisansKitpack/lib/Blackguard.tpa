COPY ~%MOD_FOLDER%/Paladin/Blackguard/2da~ ~override~
COPY ~%MOD_FOLDER%/Paladin/Blackguard/bams~ ~override~
COPY ~%MOD_FOLDER%/Paladin/Blackguard/spells~ ~override~

// ICONS

DEFINE_ACTION_FUNCTION cd_new_portrait_icon
  INT_VAR string = 0
  STR_VAR bam_file = "****"
  RET     icon
  BEGIN

  COPY_EXISTING ~statdesc.2da~ ~override~
    COUNT_2DA_ROWS 3 count
    READ_2DA_ENTRY (count - 1) 0 3 icon
    SET icon += 1

  APPEND ~statdesc.2da~ ~%icon% %string% %bam_file%~ UNLESS ~%bam_file%~

END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Aura of Despair~) STR_VAR bam_file = c0bgic01 RET c0bgic01 = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Fiendish Reckoning~) STR_VAR bam_file = c0bgic02 RET c0bgic02 = icon END

LAF ~GET_KIT_STRREF~ STR_VAR kit_name = ~BLACKGUARD~ RET kit_strref END
STRING_SET_EVALUATE kit_strref ~BLACKGUARD: The Blackguard epitomizes evil and is nothing short of a mortal fiend. The quintessential black knight, this villain carries a reputation of the foulest sort that is very well deserved. Consorting with demons and devils and serving dark deities, the Blackguard is hated and feared by all. Some people call these villains "anti-paladins" due to their completely evil nature.

Advantages
– Immune to level drain and fear.
– May turn undead as a Paladin of the same level.
– May memorize a small selection of Mage spells:
  Level 1: Blindness, Larloch's Minor Drain
  Level 2: Horror, Invisibility
  Level 3: Vampiric Touch, Hold Undead
  Level 4: Improved Invisibility, Contagion

– May use the Life Drain ability once per day. An extra use is gained at levels 8 and 12.

LIFE DRAIN: The blackguard saps the vitality of an unfortunate victim within touch range, draining 1 point of THAC0 and Damage (an additional point is drained per 5 levels up to a maximum of 5 points at 20th level), as well as 1 hit point per level, and adding it to <PRO_HISHER> maximum. The drained attributes last for 3 rounds + 1 round per level. Non-living beings, such as undead and constructs, are immune to the effects.

– May use the Poison Weapon ability once per day. Gains one use at level 1, one use at level 5, and an additional use every 5 levels thereafter.

POISON WEAPON: The blackguard is capable of coating <PRO_HISHER> weapon for the next eight hours with a slow-acting but extremely potent poison. This poison deals damage and reduces the target's Constitution immediately and every following round for at least four rounds. After four rounds have passed, the target must make a Saving Throw vs. Death or remain poisoned for another four rounds. Therefore, the poison has the potential to last indefinitely as long as the target does not make a successful Saving Throw. The target may not die of Constitution loss caused by the poison.

1st level: Target suffers two poison damage and loses one point of Constitution. No save modifier.
6th level: Target suffers four poison damage and loses one point of Constitution. Save modifier of -1.
11th level: Target suffers six poison damage and loses two points of Constitution. Save modifier of -2.
16th level: Target suffers eight poison damage and loses two points of Constitution. Save modifier of -3.
21st level: Target suffers ten poison damage and loses three points of Constitution. Save modifier of -4.

– 3rd level: Gains the Aura of Despair passive ability.

AURA OF DESPAIR: Blackguards perpetually radiate a malignant aura, inflicting negative effects on all enemies within 15 feet of <PRO_HIMHER> based on the Blackguard's level. Non-living beings, such as undead and constructs, are unaffected.	
  3rd - All enemies suffer a -1 penalty to Saving Throws and Morale.
  11th - All enemies suffer a -1 penalty to Saving Throws and Morale and a -5% penalty to all damage dealt.
  19th - All enemies suffer a -2 penalty to Saving Throws and Morale and a -10% penalty to all damage dealt.

– 11th level: May use Backstab at x2 modifier.
– 12th level: May use the Fiendish Reckoning ability once per day.

FIENDISH RECKONING: The blackguard draws upon the power of their fiendish host, gaining a +4 bonus to THAC0, +20% damage dealt from all sources, and +20 bonus hit points for 3 rounds. Killing a creature while Fiendish Reckoning is in effect heals the blackguard for 20 hit points and resets the duration. However, if no creature is killed within the initial duration, the blackguard suffers 40 magical damage and incurs a -4 penalty to THAC0 and -50% penalty to all damage dealt for 6 rounds as punishment.

– 16th level: Backstab modifier improves to x3.

Disadvantages:
– Incurs a -2 penalty to Reputation.
– Alignment restricted to evil.
– May not cast most spells of the Healing or Protection spheres.
– Does not gain Aura of Courage.
– May not cast Detect Evil.
– May not use Smite Evil.
– May not cast Protection From Evil as an innate ability.
– May not use the Lay on Hands ability.~

// backstab
APPEND ~backstab.2da~ ~BLACKGUARD 1 1 1 1 1 1 1 1 1 1 1 2 2 2 2 2 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3~
APPEND ~sneakatt.2da~ ~BLACKGUARD 1 1 1 1 1 1 1 1 1 1 1 2 2 2 2 2 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4 4 5 5 5 5 5 5 5 5 6~
APPEND ~crippstr.2da~ ~BLACKGUARD 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 1 1 2 2 2 2 2 2 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4~

// REPUTATION
ACTION_IF FILE_EXISTS_IN_GAME ~CLABPA06.2da~ THEN BEGIN
COPY ~%MOD_FOLDER%/Paladin/Blackguard/2da/CLABPA06.2da~ ~override~
END

ACTION_IF FILE_EXISTS_IN_GAME ~baldur.bcs~ THEN BEGIN
EXTEND_TOP ~baldur.bcs~ ~%MOD_FOLDER%/Paladin/Blackguard/scripts/reputation.baf~
END

ACTION_IF FILE_EXISTS_IN_GAME ~bdbaldur.bcs~ THEN BEGIN
EXTEND_TOP ~bdbaldur.bcs~ ~%MOD_FOLDER%/Paladin/Blackguard/scripts/reputation.baf~
END

ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN
EXTEND_TOP ~baldur25.bcs~ ~%MOD_FOLDER%/Paladin/Blackguard/scripts/reputation.baf~
END

COPY_EXISTING ~C0BG#00.spl~ ~override~
      LPF ADD_SPELL_EFFECT
      INT_VAR
	    opcode = 142
	    target = 2
		timing = 9
        parameter2 = c0bgic01
      END

COPY_EXISTING ~SPCL102.spl~ ~override~
SAY NAME1 ~Life Drain~
SAY UNIDENTIFIED_DESC ~Life Drain
The blackguard saps the vitality of an unfortunate victim within touch range, draining 1 point of THAC0 and Damage (an additional point is drained per 5 levels up to a maximum of 5 points at 20th level), as well as 1 hit point per level, and adding it to <PRO_HISHER> maximum. The drained attributes last for 3 rounds + 1 round per level. Non-living beings, such as undead and constructs, are immune to the effects.~

ACTION_IF FILE_EXISTS ~C0TR#20A.spl~ THEN BEGIN
COPY_EXISTING ~C0TR#20A.SPL~ OVERRIDE
LPF ALTER_SPELL_HEADER STR_VAR icon = C0BG#02 END
SAY NAME1 ~Mimic: Life Drain~
END

INCLUDE ~%MOD_FOLDER%/effects/poison_weapon.tpa~

ACTION_IF FILE_EXISTS ~C0TR#30A.spl~ THEN BEGIN
COPY_EXISTING ~C0TR#30A.SPL~ OVERRIDE
LPF ALTER_SPELL_HEADER STR_VAR icon = C0ASIN8B END
LPF ALTER_EFFECT INT_VAR match_opcode = 171 STR_VAR resource = c0as#p1 END
SAY NAME1 ~Mimic: Poison Weapon~
END

COPY_EXISTING ~C0BG#00.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      match_opcode = 139
	  parameter1 = RESOLVE_STR_REF (~Gained Passive Ability: Aura of Despair~)
END

COPY_EXISTING ~C0BG#03.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      check_globals = 1
      check_headers = 1
      match_opcode = 142 // display string
      parameter2 = c0bgic02
  END
SAY NAME1 ~Fiendish Reckoning~
SAY UNIDENTIFIED_DESC ~Fiendish Reckoning
The blackguard draws upon the power of their fiendish host, gaining a +4 bonus to THAC0, +30% damage dealt from all sources, and +20 bonus hit points for 3 rounds. Killing a creature while Fiendish Reckoning is in effect heals the blackguard for 20 hit points and resets the duration. However, if no creature is killed within the initial duration, the blackguard suffers 40 magical damage and incurs a -4 penalty to THAC0 and -50% penalty to all damage dealt for 6 rounds as punishment.~

COPY_EXISTING ~C0BG#03a.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      check_globals = 1
      check_headers = 1
      match_opcode = 142 // display string
      parameter2 = c0bgic02
  END
  
COPY_EXISTING ~C0BG#03c.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      check_globals = 1
      check_headers = 1
      match_opcode = 142 // display string
      parameter2 = c0bgic02
  END
  
COPY_EXISTING ~C0BG#03b.spl~ ~override~
SAY NAME1 ~Punishment~

//BLINDNESS
COPY_EXISTING ~SPWI106.SPL~ ~OVERRIDE/C0BG106.SPL~
WRITE_SHORT 0X34 1
WRITE_SHORT 0X1C 2
//LARLOCH'S MINOR DRAIN
COPY_EXISTING ~SPWI119.SPL~ ~OVERRIDE/C0BG119.SPL~
WRITE_SHORT 0X34 1
WRITE_SHORT 0X1C 2
//HORROR
COPY_EXISTING ~SPWI205.SPL~ ~OVERRIDE/C0BG205.SPL~
WRITE_SHORT 0X34 2
WRITE_SHORT 0X1C 2
//INVISIBILITY
COPY_EXISTING ~SPWI206.SPL~ ~OVERRIDE/C0BG206.SPL~
WRITE_SHORT 0X34 2
WRITE_SHORT 0X1C 2
//VAMPIRIC TOUCH
COPY_EXISTING ~SPWI314.SPL~ ~OVERRIDE/C0BG314.SPL~
WRITE_SHORT 0X34 3
WRITE_SHORT 0X1C 2
//HOLD UNDEAD
COPY_EXISTING ~SPWI324.SPL~ ~OVERRIDE/C0BG324.SPL~
WRITE_SHORT 0X34 3
WRITE_SHORT 0X1C 2
//IMPROVED INVISIBILITY
COPY_EXISTING ~SPWI405.SPL~ ~OVERRIDE/C0BG405.SPL~
WRITE_SHORT 0X34 4
WRITE_SHORT 0X1C 2
//CONTAGION
COPY_EXISTING ~SPWI409.SPL~ ~OVERRIDE/C0BG409.SPL~
WRITE_SHORT 0X34 4
WRITE_SHORT 0X1C 2

 ACTION_FOR_EACH dorn IN dorn dorn2 dorn4 dorn6 dorn7 dorn8 dorn9 dorn10 dorn12 dorn14 BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%dorn%.cre~ THEN BEGIN
    COPY_EXISTING ~%dorn%.cre~ ~override~
		REMOVE_KNOWN_SPELL ~SPCL102~ ~SPCL423~ ~SPCL103~
		REMOVE_MEMORIZED_SPELL ~SPCL102~ ~SPCL423~ ~SPCL103~
		BUT_ONLY
END
END

ACTION_IF FILE_EXISTS_IN_GAME ~C0PAL00.SPL~ THEN BEGIN

COPY_EXISTING ~C0PAL00.SPL~ ~OVERRIDE/C0BG#04.SPL~
LPF ALTER_EFFECT INT_VAR match_opcode = 272 STR_VAR resource = C0BG#04A END
LPF DELETE_EFFECT INT_VAR match_opcode = 142 END

COPY_EXISTING ~C0PAL01.EFF~ ~OVERRIDE/C0BG#04A.EFF~
WRITE_ASCII 0x30 ~C0BG#04B~

COPY_EXISTING ~C0PAL01.SPL~ ~OVERRIDE/C0BG#04B.SPL~
LPF DELETE_EFFECT STR_VAR match_resource = C0PAL04A END
LPF DELETE_EFFECT STR_VAR match_resource = C0PAL04B END
LPF DELETE_EFFECT STR_VAR match_resource = C0PAL04C END

END

ACTION_IF FILE_EXISTS_IN_GAME ~EEex_Actionbar.lua~ THEN BEGIN

COPY ~%MOD_FOLDER%/lua/m_c0blk1.lua~      ~override~ // Lua file read by EEex, provided by Bubbs and further edited by myself

ACTION_IF FILE_EXISTS_IN_GAME ~CLABPA06.2DA~ BEGIN
COPY_EXISTING ~CLABPA06.2DA~ OVERRIDE
  // Borrowed from Rogue Rebalancing
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 2 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 3 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 4 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 6 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 7 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 8 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 9 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 10 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 11 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 12 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 13 f_MaxLevel = 13 STR_VAR f_Entry = AP_C0BLK01 END // STEALTH
  PRETTY_PRINT_2DA
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Set Abilities for all Fighter Kits                                    //
// This allows all previously installed fighter kits to receive the      //
// benefits of your overhaul                                             //
///////////////////////////////////////////////////////////////////////////

APPEND_COL ~thiefscl.2da~ ~DELETE_TEXT DELETE_TEXT BLACKGUARD 0 0 0 100 100 0 0 0~ UNLESS ~BLACKGUARD~

COPY_EXISTING ~thiefscl.2da~ ~override~
REPLACE_TEXTUALLY ~DELETE_TEXT~ ~~
  
COPY_EXISTING ~clasiskl.2da~ ~override~
  REPLACE_TEXTUALLY ~^\([ %TAB%]*MAGE\)~ ~CD_DELETE_ME \1~ // adding column to header column
  COUNT_2DA_COLS col_count
  FOR (col = 1 ; col < col_count ; ++col) BEGIN
    READ_2DA_ENTRY 0 col col_count kit
    PATCH_IF 	("%kit%" STRING_EQUAL_CASE "BLACKGUARD") BEGIN
      READ_2DA_ENTRY 4 col col_count val
      SET_2DA_ENTRY  4 col col_count 15
      READ_2DA_ENTRY 5 col col_count val
      SET_2DA_ENTRY  5 col col_count 15
      SET col = col_count // kill loop
    END
  END  
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~ // removing it
  PRETTY_PRINT_2DA
  BUT_ONLY
  
END

///////////////////////////////////////////////////////////////////////////
// Update Kit Descriptions                                               //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH description IN 
    BGCLATXT  
    clastext  
    sodcltxt 
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%description%.2da~ BEGIN
      COPY_EXISTING ~%description%.2DA~ ~override~
      COUNT_2DA_COLS col_count
      COUNT_2DA_ROWS col_count rows
      FOR (index = 0; index < rows; ++index) BEGIN
        READ_2DA_ENTRY index 0 col_count row_name
//blackguard
        PATCH_IF "%row_name%" STRING_EQUAL_CASE "BLACKGUARD" BEGIN
          READ_2DA_ENTRY index 4 col_count blackguard_desc
          SPRINT old_blackguard ~– May turn undead as a Paladin of the same level.~
          SPRINT new_blackguard ~– May turn undead as a Paladin of the same level.
– May use Hide in Shadows.~
          GET_STRREF %blackguard_desc% blackguard_text
          INNER_PATCH_SAVE blackguard_text ~%blackguard_text%~ BEGIN
            REPLACE_TEXTUALLY ~%old_blackguard%~ ~%new_blackguard%~
          END
          INNER_ACTION BEGIN
            STRING_SET_EVALUATE ~%blackguard_desc%~ ~%blackguard_text%~
          END
		END
		
      END
      BUT_ONLY
    END
  END
  END
  
ADD_PROJECTILE ~%MOD_FOLDER%/spells/C0INAREP.PRO~ ~C0 Instant Area Enemies~

COPY_EXISTING ~C0BG#01.SPL~ OVERRIDE
  LPF ALTER_SPELL_HEADER
	INT_VAR
		projectile = C0INAREP
END
