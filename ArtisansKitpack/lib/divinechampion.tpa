ADD_KIT ~C0DC~

 // appended to CLASWEAP.2da
 ~C0DC		1           1           1           1           1           1           1           1~

 // appended column-wise to WEAPPROF.2da
//11                                    T   S           F   Q             S S
//10                                    W   C           L   U             W I
//9                             B L S   O   I   W       A   A             O N
//8                             A O H   H   M   A       I   R C   S       R G
//7         	L S           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6         	_ _       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5         	S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4         	W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3         	O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2        		R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1      	    D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
 ~C0DC			4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    
 // appended to ABCLASRQ.2da
 ~C0DC				12      9	0	0	13	17~

 // appended to ABCLSMOD.2da
 ~C0DC				0	0	0	0	0	0~

 // appended to ABDCDSRQ.2da
 ~C0DC                	0       0       0       0       0        0~

 // appended to ABDCSCRQ.2da
 ~C0DC                 	0       0       0       0       0		 0~

 // appended to ALIGNMNT.2da
 ~C0DC                  	1       1       1       1       1		1       1       1       1~

 // appended to DUALCLAS.2da
 ~C0DC                  	0       0       0       0       0       0~

 // path to your CLAB-style 2da file 
 ~%MOD_FOLDER%/Paladin/DivineChampion/C0DC.2da~

 // PC races and classes allowed to use this kit, from KITTABLE.2da
 ~K_P_H K_P_E K_P_HE K_P_G K_P_HL K_P_D K_P_HO~

 // usability flags, added to the end of the KITLIST.2da entry
 ~0x00004000     6~

 // HLA table to use; see LUABBR.2da
 ~PA0~

 // list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
 ~CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN4,2 POTN14,5 HAM07 SW1H27 STAF08~

 // Lower, mixed, and help kit names and descriptions, added to KITLIST.2da
  SAY ~divine champion~
  SAY ~Divine Champion~
  SAY ~DIVINE CHAMPION: The divine champion is the strong right arm of their deity. They serve as a guardian of holy sites, a protector of pilgrims and a leader of crusades. Though paladins fill these roles in many good-aligned churches, a divine champion can take up the banner of any deity, regardless of alignment or ethos. These holy warriors are loved by their patron's worshipers and hated by the church's enemies in equal measure.
  
Divine Champions may be of any alignment and will never fall, regardless of alignment or reputation.

Advantages:
– May achieve High Mastery (4 slots) in any weapon.
– Gains +1 to all Saving Throws at level 5 and every 5 levels gained thereafter.
– 2nd Level: May use the Smite Infidel ability once per day, gaining an additional use every 2 levels gained.

SMITE INFIDEL: The divine champion's next attack within two rounds deals 1d4 points of magic damage per 2 levels to any creature of a different moral (good, neutral, evil) alignment to the caster.

– 5th level: May use the Divine Wrath ability once per day.

DIVINE WRATH: The divine champion channels a portion of their deity’s power, gaining a +4 bonus to hit, damage and Saving Throws, +25% to all resistances, and an additional +2 bonus to Armor Class and Saving Throws against all creatures of a different moral alignment (good, neutral, evil) to their own. This ability lasts for one turn plus an additional round for each level gained.

Disadvantages:
– Does not gain additional Saving Throws with Charisma.
– Does not gain Aura of Courage.
– May not Turn Undead.
– May not use Smite Evil.
– May not use Cure Disease.
– May not cast priest spells.~

LAF fl#add_kit_ee
  STR_VAR
    kit_name = C0DC
	hpclass = ~HPWAR~
END
APPEND ~fallen.2da~ ~C0DC 0~

ACTION_IF FILE_EXISTS_IN_GAME ~C0FIG01.SPL~ BEGIN
COPY_EXISTING ~C0DC.2DA~ OVERRIDE
  // Borrowed from Rogue Rebalancing
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_MaxLevel = 5 STR_VAR f_Entry = GA_C0FIG01 END // POWER ATTACK
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 5 f_MaxLevel = 5 STR_VAR f_Entry = GA_C0FIG02 END // EXPERTISE
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 6 f_MaxLevel = 6 STR_VAR f_Entry = GA_C0ARC03 END // RAPID SHOT
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 12 f_MaxLevel = 12 STR_VAR f_Entry = AP_C0ARC03Z END
  PRETTY_PRINT_2DA
BUT_ONLY

ACTION_FOR_EACH description IN 
    clastext  
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%description%.2da~ BEGIN
      COPY_EXISTING ~%description%.2DA~ ~override~
      COUNT_2DA_COLS col_count
      COUNT_2DA_ROWS col_count rows
      FOR (index = 0; index < rows; ++index) BEGIN
        READ_2DA_ENTRY index 0 col_count row_name
//divinechampion
        PATCH_IF "%row_name%" STRING_EQUAL_CASE "C0DC" BEGIN
          READ_2DA_ENTRY index 4 col_count divinechampion_desc
          SPRINT old_divinechampion ~Disadvantages:~
          SPRINT new_divinechampion ~– 5th level: May use the Power Attack and Expertise modal abilities. Only one of the two may be activated at any time.

POWER ATTACK: The Divine Champion forgoes accuracy for heavier, more damaging blows, sacrificing 3 points of melee THAC0 to increase their melee damage by 3. The Divine Champion must have 17 or greater Strength to activate this ability.

EXPERTISE: The Divine Champion focuses on defense at the expense of accuracy, sacrificing 3 points of THAC0 to increase their Armor Class by 3. The Divine Champion must have 15 or greater Intelligence to activate this ability.

– 6th level: May use the Rapid Shot modal ability.

RAPID SHOT: The Divine Champion may make an additional ranged attack per round at a -4 penalty to hit.

– 12th level: Improved Rapid Shot: The Divine Champion’s penalty to hit while Rapid Shot is active is reduced to -2.
– 14th level: May use Improved Power Attack and Improved Expertise, which provide 6 points to the original abilities' bonuses and penalties.

Disadvantages:~
          GET_STRREF %divinechampion_desc% divinechampion_text
          INNER_PATCH_SAVE divinechampion_text ~%divinechampion_text%~ BEGIN
            REPLACE_TEXTUALLY ~%old_divinechampion%~ ~%new_divinechampion%~
          END
          INNER_ACTION BEGIN
            STRING_SET_EVALUATE ~%divinechampion_desc%~ ~%divinechampion_text%~
          END
		END
      END
      BUT_ONLY
    END
  END
  END
  
COPY ~%MOD_FOLDER%/Paladin/DivineChampion/c0dc01.spl~ ~override~
COPY ~%MOD_FOLDER%/Paladin/DivineChampion/c0dc02a.eff~ ~override~
COPY ~%MOD_FOLDER%/Paladin/DivineChampion/c0dc02a.spl~ ~override~
SAY NAME1 ~Smite Infidel~
COPY ~%MOD_FOLDER%/Paladin/DivineChampion/c0dc02.spl~ ~override~
SAY NAME1 ~Smite Infidel~
SAY UNIDENTIFIED_DESC ~Smite Infidel
The divine champion's next attack within two rounds deals 1d4 points of magic damage per 2 levels to any creature of a different moral (good, neutral, evil) alignment to the caster.~

COPY ~%MOD_FOLDER%/Paladin/DivineChampion/c0dc03.spl~ ~override~
SAY NAME1 ~Divine Wrath~
SAY UNIDENTIFIED_DESC ~Divine Wrath
The divine champion channels a portion of their deity’s power, gaining a +4 bonus to hit, damage and Saving Throws, +25% to all resistances, and an additional +2 bonus to Armor Class and Saving Throws against all creatures of a different moral alignment (good, neutral, evil) to their own. This ability lasts for one turn plus an additional round for each level gained.~

COPY ~%MOD_FOLDER%/Paladin/DivineChampion/c0dc04.spl~ ~override~